using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.WUI.Reactive
{
    public variant JsAST
    {
        | Assign         { Source : JsAST; Target : JsAST }
        | Array          { Values : list[JsAST] }
        | BinaryOperator { Op : string; Left : JsAST; Right : JsAST }
        | Break
        | Call           { Fun : JsAST; Parameters : list[JsAST]; }
        | DefFunction    { Obj : JsAST; Parameters : list[JsAST]; Body : JsAST }
        | DefValue       { Name : string; Val : JsAST }
        | For            { ItName : string; Collection : JsAST; Body : JsAST }
        | Goto           { LabelId : int }
        | If             { Cond : JsAST; Body : JsAST; ElseBody : JsAST }
        | JustIf         { Cond : JsAST; Body : JsAST }        
        | Label          { Id : int; Body : JsAST }
        | Literal        { Value : JsLiteral }
        | LocalRef       { Name : string }        
        | MethodRef      { Obj : JsAST; Method : string }
        | MethodCorLib   { Obj : string; Method : string }
        | OpCode         { Name : string; }
        | Object         { Fields : list[(string * JsAST)] }
        | Seq            { Items : list[JsAST] }
        | Switch         { Expr : JsAST; Cases : list[JsAST.Label] }
        | Ternary        { Cond : JsAST; Body : JsAST; ElseBody : JsAST }        
        | This
        | TryFinally     { Body : JsAST; Handler : JsAST }
        | Undefined
        | UnaryOperator  { Op : string; Obj : JsAST }
        | Void
        | While          { Cond : JsAST; Body : JsAST }
        | Code           { Code : string }
        | NotImpl { 
            Val : string;
            public this() {
                Val = "";
            }
        }
        
        public static Recurse(ast : JsAST, selector : JsAST -> JsAST) : JsAST 
        {
            def recurse = Recurse(_, selector);
            def newAst = selector(ast);
            match(newAst) {
            | Assign(s, t)              => JsAST.Assign(recurse(s), recurse(t))
            | Array(v)                  => JsAST.Array(v.Map(recurse))
            | BinaryOperator(o, l, r)   => JsAST.BinaryOperator(o, recurse(l), recurse(r))
            | Break                     => newAst
            | Call(func, parms)         => JsAST.Call(recurse(func), parms.Map(recurse))
            | DefFunction(obj, ps, bdy) => JsAST.DefFunction(recurse(obj), ps.Map(recurse), recurse(bdy))
            | DefValue(name, val)       => JsAST.DefValue(name, recurse(val))
            | For(it, coll, body)       => JsAST.For(it, coll, recurse(body))
            | Goto                      => newAst        
            | If(cond, then, els)       => JsAST.If(recurse(cond), recurse(then), recurse(els))
            | JustIf(cond, then)        => JsAST.JustIf(recurse(cond), recurse(then))
            | Label(id, body)           => JsAST.Label(id, recurse(body))
            | Literal                   => newAst
            | LocalRef                  => newAst
            | MethodRef(obj, meth)      => JsAST.MethodRef(recurse(obj), meth)
            | OpCode                    => newAst        
            | Seq(items)                => JsAST.Seq(items.Map(recurse))
            | Switch(expr, cases)       => JsAST.Switch(recurse(expr), cases.Map(recurse).Cast.[JsAST.Label]().NToList())
            | Ternary(cond, then, els)  => JsAST.Ternary(recurse(cond), recurse(then), recurse(els))
            | This                      => newAst
            | TryFinally(body, handler) => JsAST.TryFinally(recurse(body), recurse(handler))
            | Undefined                 => newAst
            | UnaryOperator(op, obj)    => JsAST.UnaryOperator(op, recurse(obj))
            | Void                      => newAst        
            | While(cond, body)         => JsAST.While(recurse(cond), recurse(body))
            | Code                      => newAst
            | _ => newAst
            }
        }        
    }
}
