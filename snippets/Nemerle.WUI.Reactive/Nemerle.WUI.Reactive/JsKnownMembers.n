using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Compiler.Typedtree;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.WUI.Reactive
{  
  module JsKnownMembers
  {
      public Convert(call : TExpr.Call, builder : TExpr -> JsAST) : JsAST
      {
        def result = match(call) {
        | Call(MethodRef(obj = o, meth = meth), parms, _) => ConvertMethod(o.Type.TypeInfo, builder(o), meth, parms.Map(p => builder(p.expr)))
        | Call(StaticRef(from = fromType, mem = meth) as left, parms, _) => ConvertMethod(fromType.TypeInfo, builder(left), meth, parms.Map(p => builder(p.expr)))
        | Call(OpCode as op, parms, _) => JsAST.Call(builder(op), parms.Map(p => builder(p.expr)))
        }
        result
      }
      
      public ConvertMethod(type : TypeInfo, left : JsAST, member : IMember, parms : list[JsAST]) : JsAST
      {
        match(member.Name, parms) {
        | ("op_Inequality", _) => JsAST.Call(JsAST.OpCode("!="), parms)
        | ("ToString", h :: _) when type.FullName == typeof(string).FullName => JsAST.Call(JsAST.MethodRef(h, "toString"), [])
        | ("Concat", h :: t) when type.FullName == typeof(string).FullName => JsAST.Call(JsAST.MethodRef(h, "concat"), t)
        | ("ToUpper", []) when type.FullName == typeof(string).FullName => JsAST.Call(JsAST.MethodRef(left, "toUpperCase"), [])
        | ("op_Implicit", h :: []) => h
        | ("op_Implicit", h :: t) => JsAST.Seq(h :: t)
        | ("get_Item", h :: []) when type.SystemType.FullName == typeof(List[_]).FullName => JsAST.Call(JsAST.Indexer(left), [h])
        | (".ctor", parms) => JsAST.Call(JsAST.Constructor(type), parms)
        | _ => JsAST.Call(JsAST.MethodRef(left, RemoveGetSet(member.Name)), parms)
        }
      }
      
      public ConvertField(typeName : string, member : IMember, parms : list[JsAST]) : JsAST
      {
        match(member.Name) {
        | _ when typeName == typeof(list[_].Nil).FullName => JsAST.Array([])
        | _ => 
            Message.Warning($"member.Name $(member.Name) unsupported by type $typeName");
            JsAST.NotImpl();
        }
      }
      
      public ConvertProperty(typeName : string, member : IMember, parms : list[JsAST]) : JsAST
      {
        match(member.Name) {
        | _ when typeName == typeof(list[_].Nil).FullName => JsAST.Array([])
        | _ => 
            Message.Warning($"member.Name $(member.Name) unsupported by type $typeName");
            JsAST.NotImpl();
        }
      }
      
      private RemoveGetSet(method : string) : string {
        if(method.StartsWith("get_") || method.StartsWith("set_"))
            method.Substring(4);
        else
            method;
      }
  }
}
